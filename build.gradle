plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.9'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'jacoco'
}

group = 'gr.aueb.cf'
version = '0.0.1-SNAPSHOT'
description = 'My first full-stack project'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
		vendor = JvmVendorSpec.AMAZON
	}
}

bootJar {
	archiveFileName = "cookingfactoryapp.jar"
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
	testCompileOnly {
		extendsFrom testAnnotationProcessor
	}
}


repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'com.mysql:mysql-connector-j'
	annotationProcessor 'org.projectlombok:lombok'
	testAnnotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	runtimeOnly 'com.h2database:h2'
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.9'
	implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
	runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
	runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'


}

tasks.named('test') {
	useJUnitPlatform()
	// Avoid "Sharing is only supported for boot loader classes" when agent/toolchain modify bootstrap classpath
	jvmArgs '-Xshare:off'
	// Add Byte Buddy agent so Mockito inline-mock-maker is loaded at startup (avoids self-attach warnings on newer JDKs)
	doFirst {
		def agentJar = configurations.testRuntimeClasspath.files.find { it.name.startsWith('byte-buddy-agent') }
		if (agentJar) {
			jvmArgs "-javaagent:${agentJar.absolutePath}"
		}
	}
	finalizedBy tasks.named('jacocoTestReport')
}

jacoco {
	toolVersion = '0.8.11'
}

tasks.named('jacocoTestReport') {
	dependsOn tasks.named('test')
	reports {
		xml.required = true
		html.required = true
		csv.required = false
	}
}

project.afterEvaluate {
	tasks.named('jacocoTestReport').configure {
		classDirectories.setFrom(files(classDirectories.files.collect {
			fileTree(dir: it, exclude: [
				'**/dto/**',
				'**/model/**',
				'**/CookingfactoryApplication.class',
				'**/config/**',
				'**/core/exceptions/**',
				'**/core/filters/**',
				'**/core/enums/**',
				'**/core/specifications/**'
			])
		}))
	}
}
